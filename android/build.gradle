buildscript {
    ext.kotlin_version = "1.8.10"
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    repositories {
        google()
        mavenCentral()
    }

    android {
        if (project.hasProperty('SKYWAY_RELEASE_STORE_FILE')) {
            signingConfigs {
                release {
                    storeFile file(SKYWAY_RELEASE_STORE_FILE)
                    keyPassword SKYWAY_RELEASE_KEY_PASSWORD
                    storePassword SKYWAY_RELEASE_STORE_PASSWORD
                    keyAlias SKYWAY_RELEASE_KEY_ALIAS
                }
            }
        }

        compileSdkVersion 33
        buildToolsVersion "30.0.3"

        defaultConfig {
            minSdkVersion 16
            targetSdkVersion 33

            archivesBaseName = 'sambaza'
            group = 'com.auterion'
            versionCode 4
            version "1.0.3"

            ndk {
                abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
            }
        }

        buildTypes {
            release {
                minifyEnabled false
            }
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_11
            targetCompatibility JavaVersion.VERSION_11
        }
        kotlinOptions {
            jvmTarget = '11'
        }

        testOptions {
            unitTests.all {
                testLogging {
                    outputs.upToDateWhen { false }
                    events "passed", "failed", "skipped", "standardError"
                    showCauses true
                    showExceptions true
                }
            }
        }

        ndkVersion '25.1.8937393'
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
        testImplementation 'junit:junit:4.13.2'
    }

    if (project.hasProperty('ossrhUsername')
            && project.hasProperty('ossrhPassword')) {
        afterEvaluate {
            publishing {
                publications {
                    release(MavenPublication) {
                        from components.release

                        pom {
                            name = 'Sambaza'
                            packaging = 'aar'
                            description = 'RTSP proxy'
                            url = 'https://github.com/auterion/sambaza'

                            scm {
                                connection = 'scm:git:https://github.com/auterion/sambaza'
                                developerConnection = 'scm:git:https://github.com/jonasvautherin'
                                url = 'https://github.com/auterion/sambaza'
                            }

                            licenses {
                                license {
                                    name = 'GNU Lesser General Public License v2.1 only'
                                    url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1-standalone.html'
                                }
                            }

                            developers {
                                developer {
                                    id = 'jonasvautherin'
                                    name = 'Jonas Vautherin'
                                    email = 'jonas.vautherin@gmail.com'
                                }
                            }
                        }
                    }
                }
                repositories {
                    maven {
                        url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"

                        credentials {
                            username = ossrhUsername
                            password = ossrhPassword
                        }
                    }
                }
            }

            signing {
                useGpgCmd()
                sign publishing.publications.release
            }
        }
    }
}

if (project.hasProperty('SKYWAY_RELEASE_STORE_FILE')) {
    tasks.whenTaskAdded { task ->
        if (task.name == 'assembleRelease' || task.name == 'bundleReleaseAar') {
            def aarPath = "${project.buildDir}/outputs/aar/sambaza-release.aar"

            task.doLast {
                ant.signjar(
                        alias: android.signingConfigs.release.keyAlias,
                        jar: aarPath,
                        keystore: android.signingConfigs.release.storeFile,
                        storepass: android.signingConfigs.release.storePassword,
                        keypass: android.signingConfigs.release.keyPassword,
                        preservelastmodified: 'true')

                ant.verifyjar(
                        alias: android.signingConfigs.release.keyAlias,
                        jar: aarPath,
                        keystore: android.signingConfigs.release.storeFile,
                        storepass: android.signingConfigs.release.storePassword,
                        keypass: android.signingConfigs.release.keyPassword)
            }
        }
    }
}

